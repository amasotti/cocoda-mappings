# Wikidata mapping properties and corresponding knowledge organization systems (KOS)
SELECT DISTINCT ?prop ?kos ?bartoc ?kosLabel ?propLabel ?size ?sizeAt
WHERE
{

  # KOS with BARTOC ID
  ?kos wdt:P2689 ?bartoc 

  # corresponding Wikidata property
  { ?kos wdt:P1687 ?prop } UNION { ?prop wdt:P1629 ?kos }
  # or corresponding Wikidata of its part (indirect connection)
  UNION
  { ?kos wdt:P527 [ wdt:P1687 ?prop ] }
  UNION
  { ?id wdt:P1687 ?prop; wdt:P361 ?kos }

  # Wikidata property for authority control
  ?prop wdt:P31/wdt:P279* wd:Q18614948

  # number of parts at given time (with preferred rank)
  OPTIONAL {     
    ?prop p:P1114 ?stm. 
    ?stm ps:P1114 ?size .
    OPTIONAL { ?stm pq:P585 ?sizeAt }
  }
  
  # names
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,de" }
}
ORDER BY xsd:integer(?bartoc) ?sizeAt
